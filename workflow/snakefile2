# for re-assemling from raw input
from snakemake.io import touch, directory, temp, expand

# configfile: specify via command line
# command to run the pipeline on 14 threads and 10Gb of RAM:
# snakemake --use-conda --cores 14 --resources mem_mb=10000
# snakemake --dag results/final/DA63360_all.done | dot -Tpng > dag.png

rule all:
    input:
        expand("results/final/{strain}_all.re.done", strain=config['strains'])

rule unicycler:
    input:
        short_read_1 = "resources/data_raw/{strain}/Illumina/renamed/{strain}_1.fq.gz",
        short_read_2 = "resources/data_raw/{strain}/Illumina/renamed/{strain}_2.fq.gz",
        long_read = "resources/data_raw/{strain}/Nanopore/{strain}_all.fastq.gz"
    output:
        directory("results/reassemblies/assemblies/{strain}")
    threads: 16
    message:
        "executing Unicycler with {threads} threads on {wildcards.strain} raw reads"
    log:
        "results/logs/{strain}_unicycler.re.log"
    conda: "envs/unicycler.yaml"
    shell:
        "unicycler -1 {input.short_read_1} -2 {input.short_read_2} -l {input.long_read} -t {threads} -o {output} &> {log}"

rule QC_assembly:
    input:
        "results/reassemblies/assemblies/{strain}",
        "resources/busco_downloads"
    output:
        directory("results/reassemblies/qualcheck_assembly/{strain}")
    threads: 16
    message: "executing BUSCO and QUAST with {threads} threads on {wildcards.strain} reassembly"
    conda: "envs/busco_quast.yaml"
    params: tax_dataset="gammaproteobacteria_odb10"
    script:
        "scripts/QC_assembly.py"

rule bwa_map:
    input:
        assembly_dir = "results/reassemblies/assemblies/{strain}",
        short_read_1= "resources/data_raw/{strain}/Illumina/renamed/{strain}_1.fq.gz",
        short_read_2 = "resources/data_raw/{strain}/Illumina/renamed/{strain}_2.fq.gz"
    output:
        # an output file marked as temp is deleted after all rules that use it as an input are completed
        temp("results/reassemblies/mapping/{strain}/assembly.sam")
    threads: 16
    message: "executing BWA with {threads} threads on {wildcards.strain} reassembly"
    log: mem = "results/logs/{strain}_bwa_mem.re.log",
         index = "results/logs/{strain}_bwa_index.re.log"
    conda: "envs/bwa.yaml"
    shell:
        "bwa index {input.assembly_dir}/assembly.fasta &> {log.index} && "
        "bwa mem -t {threads} {input.assembly_dir}/assembly.fasta {input.short_read_1} {input.short_read_2} -o {output} &> {log.mem}"

rule samtools:
    input:
        "results/reassemblies/mapping/{strain}/assembly.sam"
    output:
        "results/reassemblies/mapping/{strain}/assembly.bam"
    threads: 18
    message: "executing SAMTOOLS: VIEW-SORT with {threads} threads on {wildcards.strain} mapping file"
    log: "results/logs/{strain}_samtools.re.log"
    conda: "envs/samtools.yaml"
    shell:
        "samtools view -b {input} | samtools sort -o {output} -O BAM -@ {threads} &> {log} && samtools index {output}"

rule unmapped:
    input:
        "results/reassemblies/mapping/{strain}/assembly.bam"
    output:
        r1 = "results/reassemblies/mapping/{strain}/unmapped_1.fastq",
        r2 = "results/reassemblies/mapping/{strain}/unmapped_2.fastq"
    threads: 18
    message: "executing SAMTOOLS: VIEW-FASTQ with {threads} threads on {wildcards.strain} BAM file"
    log: "results/logs/{strain}_unmapped.re.log"
    conda: "envs/samtools.yaml"
    shell:
        "samtools view -@ {threads} -u -f 12 -F 256 {input} | samtools fastq -1 {output.r1} -2 {output.r2} -@ {threads} &> {log}"

rule plasmid_assembly:
    input:
        r1 = "results/reassemblies/mapping/{strain}/unmapped_1.fastq",
        r2 = "results/reassemblies/mapping/{strain}/unmapped_2.fastq"
    output:
        directory("results/reassemblies/plasmids/{strain}")
    threads: 18
    message: "executing SPAdes in plasmid mode with {threads} threads on unmapped reads of {wildcards.strain}"
    log: "results/logs/{strain}_spades.re.log"
    conda: "envs/spades.yaml"
    shell:
        # || true prevents the rule from failing when spades throws an error; this happens when unmapped files are too small
        "spades.py --plasmid -1 {input.r1} -2 {input.r2} -t {threads} -o {output} &> {log} || true"

rule assembly_summary:
    input:
        "results/reassemblies/assemblies/{strain}",
        "results/reassemblies/plasmids/{strain}"
    output:
         "results/reassemblies/assemblies_joined/{strain}/summary.tsv"
    threads: 1
    message: "summarizing unicycler and SPAdes assemblies of strain {wildcards.strain}"
    log: "results/logs/{strain}_assembly_summary.re.log"
    script:
        "scripts/assembly_summary.py"

rule join_assemblies:
    input:
        "results/reassemblies/assemblies/{strain}",
        "results/reassemblies/plasmids/{strain}"
    output:
        "results/reassemblies/assemblies_joined/{strain}/assembly.fasta"
    threads: 1
    message: "joining Unicycler assembly and SPAdes plasmid assembly together, strain {wildcards.strain}"
    log: "results/logs/{strain}_joiner.re.log"
    script:
        "scripts/join_two_fastas.py"

rule genome_annotation:
    # proteins (prodigal) and rRNA (barrnap)
    input:
        "results/reassemblies/assemblies_joined/{strain}/assembly.fasta"
    output:
        directory("results/reassemblies/annotations/{strain}/prokka")
    threads: 18
    message: "executing PROKKA with {threads} threads on full re-assembly of {wildcards.strain}"
    log: "results/logs/{strain}_prokka.re.log"
    conda: "envs/prokka.yaml"
    params: centre="UU", minlen="200", genus="Escherichia", species="coli"
    shell:
        # skip tRNAs search?
        "prokka --addgenes --addmrna --compliant --notrna --outdir {output} --prefix {wildcards.strain}_genomic --centre {params.centre} --genus {params.genus} "
        "--species {params.species} --strain {wildcards.strain} --kingdom Bacteria --cpus {threads} "
        "--mincontiglen {params.minlen} {input} &> {log}"

rule rename_gbk:
    input:
        "results/reassemblies/annotations/{strain}/prokka"
    output:
        "results/reassemblies/annotations/{strain}/prokka_renamed/{strain}_genomic.gbk"
    params:
        filename="{strain}_genomic.gbk"
    script:
        "scripts/rename_genomic_gbk.py"

rule trna_annotation:
    # tRNA genes only - tRNAScan-SE
    input:
        "results/reassemblies/assemblies_joined/{strain}/assembly.fasta"
    output:
        # TODO: use multiext function
        general = "results/reassemblies/annotations/{strain}/trna/trna_gen.txt",
        struct = "results/reassemblies/annotations/{strain}/trna/trna_struct.txt",
        iso = "results/reassemblies/annotations/{strain}/trna/trna_iso.txt",
        stats = "results/reassemblies/annotations/{strain}/trna/trna_stat.txt",
        bed = "results/reassemblies/annotations/{strain}/trna/trna_coords.bed",
        gff = "results/reassemblies/annotations/{strain}/trna/trna_feat.gff",
        fasta = "results/reassemblies/annotations/{strain}/trna/trna_seq.fasta"
    threads: 18
    message: "executing tRNAScan-SE with {threads} threads on full assembly of {wildcards.strain} strain"
    log: "results/logs/{strain}_trnascan.re.log"
    conda: "envs/trnascan.yaml"
    shell:
        "tRNAscan-SE -B --forceow -o {output.general} -f {output.struct} -s {output.iso} -m {output.stats} -b {output.bed} "
        "-j {output.gff} -a {output.fasta} -l {log} --thread {threads} {input} &> {log}"

rule join_annotations:
    input:
        prokka="results/reassemblies/annotations/{strain}/prokka",
        trnascan="results/reassemblies/annotations/{strain}/trna/trna_seq.fasta"
        # mode="annotation"
    output:
        "results/reassemblies/annotations/{strain}/joined/annotation.fasta"  # it's not supposed to be used as input for RGI tool
    script:
        "scripts/join_two_fastas.py"

rule resistance_genes:
    # it doesn't work with tRNAs
    # ALSO: rgi load --card_json ./card_database/card.json --local
    input:
        "results/reassemblies/annotations/{strain}/prokka"
    output:
        "results/reassemblies/resistance_genes/{strain}/rgi_table.txt" # IT'S JUST A PREFIX!
    threads: 18
    message: "executing RGI with {threads} threads on predicted genes/proteins from {wildcards.strain}"
    log: "results/logs/{strain}_rgi.re.log"
    conda: "envs/rgi.yaml"
    shell:
        "output=$(echo '{output}' | cut -d'.' -f 1) && "
        "rgi main --input_sequence {input}/{wildcards.strain}_genomic.faa --output_file $output  "
        "--input_type protein --local  --num_threads {threads} --include_loose --clean &> {log}"

rule rg2gbk:
    input:
        "results/reassemblies/resistance_genes/{strain}/rgi_table.txt",
        "results/reassemblies/annotations/{strain}/prokka"
    output:
        "results/annotations/{strain}/resistance_genes/{strain}_resistance_genes.gbk"
    params: filter_criterion="Loose"
    script:
        "scripts/rgi2gff.py"
